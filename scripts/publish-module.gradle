apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'


tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

task androidJavadocsJar(type: Jar, dependsOn: dokkaJavadoc){
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.named("dokkaJavadoc") {
    mustRunAfter("kaptReleaseKotlin")
}

tasks.named("androidSourcesJar") {
    mustRunAfter("kaptReleaseKotlin")
}


tasks.named("androidSourcesJar") {
    mustRunAfter("generateMetadataFileForReleasePublication")
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                artifact androidSourcesJar
                artifact androidJavadocsJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'SDK to interface with a Scatterbrain router'
                    url = 'https://github.com/Scatterbrain-DTN/ScatterbrainSDK'
                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                            comments = 'A business-friendly OSS license'
                        }
                    }
                    developers {
                        developer {
                            id = 'gnu3ra'
                            name = 'Alex Ballmer'
                            email = 'gnu3ra@riseup.net'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/Scatterbrain-DTN/ScatterbrainSDK.git'
                        developerConnection = 'scm:git:ssh://github.com/Scatterbrain-DTN/ScatterbrainSDK.git'
                        url = 'https://github.com/Scatterbrain-DTN/ScatterbrainSDK/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}